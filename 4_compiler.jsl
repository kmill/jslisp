;;; 4_compiler.jsl
;;; Compiler to be compiled by 3_compiler

;; This should be compiled with
;; node 3_compiler.js 4_base.jsl 4_compiler.jsl -o 4_compiler.js

(def compiler-version ,(++ "4_compiler "
                           "built on "
                           (let (d (js:js "new Date()"))
                             (++ (+ 1 (js:call d "getMonth")) "/"
                                 (js:call d "getDate") "/"
                                 (js:call d "getFullYear")))))

;;; Streams

(def eof '(eof))
(defun (eof? x) (= x eof))

(defstruct (SourcePos source line col))
(defun (SourcePos-show pos)
  (destruct pos
    (SourcePos source line col)
      (++ source ":" line ":" col)
    (error (++ "Not a SourcePos: " (repr pos)))))

(defun (stream:make source s)
  (let (i 0
        len (s:length s)
        line 1
        col 0)
    ; update line and col based on character.
    (defun (advance c)
      (switch c
        "\n" (do (inc! line)
                 (set! col 0))
        (do (inc! col)))
      c)
    (dfun :as self
      (:eof?)
        (>= i len)
      (:peek)
        (if (< i len)
          (s:charAt s i)
          eof)
      (:read)
        (if (< i len)
          (do1 (advance (s:charAt s i))
               (inc! i))
          eof)
      (:read-until_ pred)
        ; doesn't return anything
        (while (let (c (self :peek))
                 (not (or (eof? c) (pred c))))
          (self :read))
      (:read-until pred)
        (let (start i)
          (self :read-until_ pred)
          (s:slice s start i))
      (:back-up) ; doesn't try hard to back up. used for errors
        (do (dec! i)
            (dec! col))
      (:pos)
        (SourcePos source line col))))

(defun (stream:error s msg)
  (error (++ (SourcePos-show (s :pos)) ": " msg)))
(defun (stream:error-expecting s expect)
  (stream:error s (++ "Expecting '" expect "'.")))

; Tries to read a particular character. Returns true (and reads) or
; false (if it doesn't)
(defun (stream:maybe-read s char)
  (and (= char (s :peek))
       (do (s :read) @true)))
(defun (stream:expect-read s char)
  (or (stream:maybe-read s char)
      (stream:error-expecting s char)))

(defun (stream:read-until-chars s chars)
  (s :read-until (fun (c) (s:contains chars c))))
(defun (stream:read-until-chars_ s chars)
  (s :read-until_ (fun (c) (s:contains chars c))))
(defun (stream:read-while-chars_ s chars)
  (s :read-until_ (fun (c) (not (s:contains chars c)))))

;;; Reader

(def reader-whitespace-chars " \t\n\r")

(defun (read:eat-spaces s)
  (stream:read-while-chars_ s reader-whitespace-chars)
  (while (= ";" (s :peek)) ; and eat comments, too
    (stream:read-until-chars_ s "\n")
    (stream:read-while-chars_ s reader-whitespace-chars)))


(def reader-non-symbol-chars reader-whitespace-chars)

(defun (make-reader-table)
  (let (table (dict)
        defaultHandler (fun (stream chars)
                         (stream:error stream "Unexpected.")))
    (dfun :as self
      (:read stream)
        (self :_read stream "")
      (:_read stream readChars)
        (let* (c (stream :peek)
               st (dict:get table c @null))
          (if (null? st)
            (defaultHandler stream readChars)
            (st :_read stream (++ readChars (stream :read)))))
      (:set-default-handler! dh)
        (set! defaultHandler dh)
      (:add-handler! f chars)
        (if (= 0 (s:length chars))
              (self :set-default-handler! f)
            otherwise
              (let (st (dict:get table (s:charAt chars 0) @null))
                (when (null? st)
                  (set! st (make-reader-table))
                  (dict:set! table (s:charAt chars 0) st))
                (st :add-handler! f (s:slice chars 1))))
      (:get-bound-chars)
        (s:join "" (for entry in (dict:items table)
                     (++ (first entry) ((second entry) :get-bound-chars)))))))

(def global-reader-table (make-reader-table))

; Adds handler to the table and updates the non-symbol-characters
(defun (set-reader-macro! chars handler)
  (global-reader-table :add-handler! handler chars)
  (set! reader-non-symbol-chars (++ (global-reader-table :get-bound-chars) reader-whitespace-chars))
  @null)



(defun (read:sexp-tail open close s)
  (def startpos (s :pos))
  (read:eat-spaces s)
  (if (s :eof?)
        (stream:error s (++ "Expecting '" close "' to match '" open "' near "
                            (SourcePos-show startpos) "."))
      (stream:maybe-read s close)
        '()
      otherwise
        (let (h (read s))
          (read:eat-spaces s)
          (if (stream:maybe-read s "#")
            (do1 (cons h (read s))
                 (read:eat-spaces s)
                 (stream:expect-read s close))
            (cons h (read:sexp-tail open close s))))))

(set-reader-macro! "("
  (fun (stream chars)
    (read:sexp-tail "(" ")" stream)))
(set-reader-macro! ")"
  (fun (stream chars)
    (stream :back-up)
    (stream:error stream "Unexpected ')'.")))
(set-reader-macro! "#"
  (fun (stream chars)
    (stream :back-up)
    (stream:error stream "Unexpected '#'.")))

(set-reader-macro! "["
  (fun (stream chars)
    (defun (consify lst)
      (if (nil? lst)  ''()
          (cons? lst) `(cons ,(head lst) ,(consify (tail lst)))
          otherwise   lst))
    (consify (read:sexp-tail "[" "]" stream))))
(set-reader-macro! "]"
  (fun (stream chars)
    (stream :back-up)
    (stream:error stream "Unexpected ']'.")))

(set-reader-macro! "{"
  (fun (stream chars)
    (cons 'dict (read:sexp-tail "{" "}" stream))))
(set-reader-macro! "}"
  (fun (stream chars)
    (stream :back-up)
    (stream:error stream "Unexpected '}'.")))

(defun (read:string quot s)
  (defun (_read-next)
    (stream:read-until-chars s (++ "\\" quot)))
  (let (r (make-array))
    (a:push! r (_read-next))
    (while (not (= quot (s :peek)))
      (when (s :eof?) (stream:error-expecting s quot))
      (s :read) ; eat backslash
      (let (esc (switch (s :peek)
                  "\\"  "\\"
                  quot  quot
                  "n"   "\n"
                  "t"   "\t"
                  "r"   "\r"
                  (stream:error s "Unknown escape code.")))
        (s :read) ; delayed so error is in right spot
        (a:push! r esc)
        (a:push! r (_read-next))))
    (s :read)
    (a:join r "")))

(set-reader-macro! "\""
  (fun (stream chars)
    (read:string "\"" stream)))
(set-reader-macro! "|"
  (fun (stream chars)
    (symbol (read:string "|" stream))))

(defun (read:token s)
  (stream:read-until-chars s reader-non-symbol-chars))

(set-reader-macro! ""
  (fun (stream chars)
    "Default token reader."
    (let (t (read:token stream))
      (if (= "" t)
        (stream:error stream "Expecting symbol or number.")
        (let (val (js:toNum t))
          (if (and (NaN? val) (!= "NaN" t))
            (symbol t)
            val))))))

(set-reader-macro! "@"
  (fun (stream chars)
    (switch (read:token stream) :as t
      "true"      @true
      "false"     @false
      "null"      @null
      "undefined" (js:js "void 0")
      otherwise
        (do (stream :back-up)
            (stream:error stream (++ "Unexpected literal " t))))))

(set-reader-macro! "'"
  (fun (stream chars)
    (list 'quote (read stream))))
(set-reader-macro! "`"
  (fun (stream chars)
    (list 'quasiquote (read stream))))
(set-reader-macro! ","
  (fun (stream chars)
    (list 'unquote (read stream))))
(set-reader-macro! ",#"
  (fun (stream chars)
    (list 'unquote-splicing (read stream))))


(defun (read s)
  (read:eat-spaces s)
  (if (s :eof?)
        (stream:error s "Unexpected end of input.")
      otherwise
        (global-reader-table :read s)))

(defun (read:has-another? s)
  (read:eat-spaces s)
  (not (s :eof?)))


; Makes a global environment in which javascript code can be
; evaluated.
(defun (make-global-env parent)
  (let* (globals2 ((js:js "function (d) {
                             function Subenv() {}
                               Subenv.prototype = d;
                               return new Subenv();
                           }") parent)
         closure ((js:js "function (globals) {
                            return function () {
                              return eval(arguments[0]);
                            };
                          }") globals2)
         all-code '()
         warned-globals {})
    (dfun :as self
      (:make-subenv)
        (make-subenv self)
      (:eval_ code)
        (closure code)
      (:eval code)
        (do (set! all-code (cons code all-code))
            (self :eval_ code))
      (:code)
        (s:join "\n\n" (reversed all-code))
      (:lookup k)
        (let (v (prop (closure "globals") k))
          (if (= (js:js "void 0") v)
              (error (++ "No such global " k))
              v))
      (:has? k)
        (!= (js:js "void 0") (prop (closure "globals") k))
      ; TODO remove these two
      ('has? k) (self :has? k)
      ('lookup k) (self :lookup k)
      (:set! k v)
        (dict:set! (closure "globals") k v)
      (:unwarn-var k)
        (dict:set! warned-globals k @true)
      (:clear-warned)
        (set! warned-globals {})
      (:as-js-env)
        (dfun
          (:lookup k)
            (do (when (and (not (self :has? k))
                           (not (dict:has? warned-globals k)))
                  (dict:set! warned-globals k @true)
                  (println "Warning: undefined global variable " k))
                (midir:prop (midir:lookup "globals")
                            (midir:lit (symbol->string k))))
          (:has? k)
            @true
          (:set! k v)
            (error "Cannot set to global environment as js-env.")))))

(def global-env (make-global-env globals))

; Defines a lexical subenvironment
(defun (make-subenv parent)
  (let* (globals2 ((js:js "function (d) {
                             function Subenv() {}
                             Subenv.prototype = d;
                             return new Subenv();
                           }") (parent :eval_ "globals"))
         closure ((js:js "function (globals) {
                            return function () {
                              return eval(arguments[0]);
                            };
                          }") globals2))
    (dfun :as self
      (:make-subenv)
        (make-subenv self)
      (:eval code)
        (self :eval_ code)
      (:eval_ code)
        (closure code)
      (:lookup k)
         (let (v (prop (closure "globals") k))
           (if (= (js:js "void 0") v)
               (error (++ "No such local or global " k))
               v))
      (:has? k)
        (!= (js:js "void 0") (prop (closure "globals") k))
      ; TODO remove these two
      ('has? k) (self :has? k)
      ('lookup k) (self :lookup k)
      (:set! k v)
        (dict:set! (closure "globals") k v))))

(defun (macro? x)
  (and (function? x) (= (js:prop x "macro") @true)))
(defun (macro-type x)
  (js:prop x "macro-type"))
(defun (form-macro? x)
  (and (macro? x) (= "form" (macro-type x))))
(defun (symbol-macro? x)
  (and (macro? x) (= "symbol" (macro-type x))))

(defun (inner-add-macro! name f)
  (global-env :set! name f)
  (set! (prop f "macro") @true)
  (set! (prop f "macro-type") "form")
  @null)

(defun (inner-add-symbol-macro! name f)
  (global-env :set! name f)
  (set! (prop f "macro") @true)
  (set! (prop f "macro-type") "symbol")
  @null)

(defmac (defmac-inner v # body)
  `(inner-add-macro! ',(head v)
     (fun ,(tail v) ,#body)))

; Marks that the macro expansion is complete. This is for low-level
; macros to signal that they haven't expanded into something
; expandable.
(defstruct (NoExpand expr))

(defun (self-evaluating? s)
  (or (number? s) (boolean? s) (null? s) (string? s) (keyword? s) (undefined? s)))

(defun (method-symbol? s)
  "Checks whether a symbol represents a method or property accessor."
  (and (symbol? s) (= "." (s:charAt (symbol->string s) 0))))
(defun (expand-method-symbol s)
  (if (= ".." (s:slice (symbol->string s) 0 2))
    `(js:js ,(++ "function (o) {\n"
                 "  return o[" (js-escape-string (s:slice (symbol->string s) 2)) "];\n"
                 "}"))
    `(js:js ,(++ "function (o) {\n"
                 "  return o[" (js-escape-string (s:slice (symbol->string s) 1)) "].apply(o, Array.prototype.slice.call(arguments, 1));\n"
                 "}"))))

; For testing macro expansion
(defun (macro-expand-1 s)
  (destruct (macro-expand-step s global-env)
    (NoExpand expr)  expr
    exp              exp))

; The real deal
(defun (macro-expand s &opt (env global-env))
  (destruct (macro-expand-step s env)
    (NoExpand expr)  expr
    exp              (macro-expand exp env)))

(defun (macro-expand-step s env)
  (if (self-evaluating? s)  (NoExpand s)
      (symbol? s)           (macro-expand-symbol s env)
      (cons? s)             (let (cs (compiler-macro-expand s env))
                              (if (= cs s)
                                (macro-expand-form s env)
                                (macro-expand-step cs env)))
      (error (++ "Unknown expression to macro expand " (repr s)))))

(defun (macro-expand-symbol s env)
  (if (and (env :has? s)
           (symbol-macro? (env :lookup s)))
        ((env :lookup s) s env)
      (method-symbol? s)
        (expand-method-symbol s)
      otherwise
        (NoExpand s)))

(defun (macro-expand-form s env)
  (if (and (env :has? (head s))
           (form-macro? (env :lookup (head s))))
        ((env :lookup (head s)) s env)
      otherwise
        (NoExpand (map (fun (e) (macro-expand e env)) s))))

(defun (compiler-macro-expand s env)
  (destruct (compiler-macro-expand-step s env)
    (NoExpand expr) expr
    exp             (compiler-macro-expand exp env)))

(defun (compiler-macro-expand-step s env)
  (if (self-evaluating? s)  (NoExpand s)
      (symbol? s)           (NoExpand s)
      (cons? s)             (compiler-macro-expand-form s env)
      otherwise             (NoExpand s)))

(defun (compiler-macro-expand-form s env)
  (if (and (env :has? (head s))
           (has-compiler-macro? (env :lookup (head s))))
        ((compiler-macro (env :lookup (head s))) s env)
      otherwise
        (NoExpand s)))

;;; Low-level forms

;; Metaprogramming
(inner-add-macro! 'quote
  (fun (s env)
    (NoExpand s)))
(inner-add-macro! 'quasiquote
  (fun (s env)
    (quasiquote-expand (nth s 1) 0)))
(inner-add-macro! 'unquote
  (fun (s env)
    (eval-in-env (nth s 1) env)))

; initially stolen from http://3e8.org/pub/scheme/doc/Quasiquotation%20in%20Lisp%20(Bawden).pdf

(defun (quasi-append # things)
  (defun (is-empty? s)
    (destruct s
      ['quote '()] @true
      ['list]      @true
      _            @false))
  (destruct things
    '() ''()
    [x] x
    [x # xs]
      :when (is-empty? x)
        (apply quasi-append xs)
      :when (is-empty? (head xs))
        (apply quasi-append (cons x (tail xs)))
    [['cons x y] # xs]
      (quasi-cons x (apply quasi-append (cons y xs)))
    [['list x # xs] # ys]
      (quasi-cons x (apply quasi-append (cons `(list ,#xs) ys)))
    [x # xs]
      `(append2 ,x ,(apply quasi-append xs))))

(defun (quasi-cons x xs)
  (destruct xs
    ['list # xss]  `(list ,x ,#xss)
    _              `(cons ,x ,xs)))
(defun (quasi-list # things)
  `(list ,#things))

(defun (quasiquote-expand s depth)
  (destruct s
    ['quasiquote # xs]
      (quasi-cons ''quasiquote
                  (quasiquote-expand xs (+ depth 1)))
    ['unquote # xs]
      :when (> depth 0)
        (quasi-cons ''unquote
                    (quasiquote-expand xs (- depth 1)))
    ['unquote x]
      x
    ['unquote # xs]
      `',s
    ['unquote-splicing # xs]
      (if (> depth 0)
            (quasi-cons ''unquote-splicing
                        (quasiquote-expand xs (- depth 1)))
          otherwise
            `',s)
    [x 'unquote # xs]
      (quasiquote-expand-list x depth xs)
    [x # xs]
      (quasi-append (quasiquote-expand-list x depth ''())
                    (quasiquote-expand xs depth))
    _
      `',s))

(defun (quasiquote-expand-list s depth end)
  (if (cons? s)
        (destruct s
          ['quasiquote # xs]
            (quasi-cons (quasi-cons ''quasiquote
                                    (quasiquote-expand xs (+ depth 1)))
                        end)
          ['unquote # xs]
            (if (> depth 0)
                  (quasi-cons (quasi-cons ''unquote
                                          (quasiquote-expand xs (- depth 1)))
                              end)
                otherwise
                  (quasi-cons (nth s 1) end))
          ['unquote-splicing # xs]
            (if (> depth 0)
                  (quasi-cons (quasi-cons ''unquote-splicing
                                          (quasiquote-expand xs (- depth 1)))
                              end)
                otherwise
                  (if (and (cons? end) (= 'quote (head end)) (nil? (nth end 1)))
                    (apply quasi-append xs)
                    `(as-improper ,(apply quasi-append xs) ,end)))
          _
            (quasi-cons (quasi-append (quasiquote-expand-list (head s) depth ''())
                                      (quasiquote-expand (tail s) depth))
                        end))
      otherwise
        `(cons ',s ,end)))


;; Javascript forms
(inner-add-macro! 'js:def
  (fun (s env)
    (NoExpand (list 'js:def (nth s 1) (macro-expand (nth s 2) env)))))
(inner-add-macro! 'js:if
  (fun (s env)
    (NoExpand (list 'js:if
                    (macro-expand (nth s 1) env)
                    (macro-expand (nth s 2) env)
                    (macro-expand (nth s 3) env)))))
(inner-add-macro! 'js:while
  (fun (s env)
    (NoExpand (list 'js:while
                    (macro-expand (nth s 1) env)
                    (macro-expand (nth s 2) env)))))
(inner-add-macro! 'js:return
  (fun (s env)
    (NoExpand (list 'js:return (macro-expand (nth s 1) env)))))
(inner-add-macro! 'js:break
  (fun (s env) (NoExpand s)))
(inner-add-macro! 'js:continue
  (fun (s env) (NoExpand s)))
(inner-add-macro! 'js:seq
  (fun (s env)
    (NoExpand (list 'js:seq (map (fun (e) (macro-expand e env)) (nth s 1))))))
(inner-add-macro! 'js:js
  (fun (s env) (NoExpand s)))
(inner-add-macro! 'js:code
  (fun (s env) (NoExpand s)))
(inner-add-macro! 'js:binop
  (fun (s env)
    (NoExpand (list 'js:binop (nth s 1)
                    (macro-expand (nth s 2) env)
                    (macro-expand (nth s 3) env)))))
(inner-add-macro! 'js:unop
  (fun (s env)
    (NoExpand (list 'js:unop (nth s 1) (macro-expand (nth s 2) env)))))
(inner-add-macro! 'js:prop
  (fun (s env)
    (NoExpand (list 'js:prop
                    (macro-expand (nth s 1) env)
                    (macro-expand (nth s 2) env)))))
(inner-add-macro! 'js:try
  (fun (s env)
    (NoExpand (list 'js:try
                    (macro-expand (nth s 1) env)
                    (nth s 2)
                    (let (env2 (env :make-subenv))
                      (env2 :set! (nth s 2) @null)
                      (macro-expand (nth s 3) env2))
                    (if (null? (nth s 4))
                        @null
                        (macro-expand (nth s 4) env))))))
(inner-add-macro! 'js:throw
  (fun (s env)
    (NoExpand (list 'js:throw
                    (macro-expand (nth s 1) env)))))
(inner-add-macro! 'js:function
  (fun (s env)
    (if (cons? (nth s 1))
          (list 'js:function @null (nth s 1) (nth s 2))
        otherwise
          (let (env2 (env :make-subenv))
            (for_ k in (nth s 2)
              (env2 :set! k @null))
            (NoExpand (list 'js:function (nth s 1) (nth s 2)
                            (macro-expand (nth s 3) env2)))))))

;;; Compiling to midir

; The mid-IR gives access to javascript control-flow primitives

; expressions
(defstruct (midir:js code))
(defstruct (midir:binop op expl expr))
(defstruct (midir:unop op exp))
(defstruct (midir:prop exp prop))
(defstruct (midir:lit s))
(defstruct (midir:lookup s))
(defstruct (midir:funcall f args))
(defstruct (midir:function mname args body))
(defstruct (midir:nop))

; statements
(defstruct (midir:code code))
(defstruct (midir:expr expr))
(defstruct (midir:def var expr))
(defstruct (midir:if cexpr bcons balt))
(defstruct (midir:while expr body))
(defstruct (midir:return expr))
(defstruct (midir:break))
(defstruct (midir:continue))
(defstruct (midir:throw expr))
(defstruct (midir:try ts var cs mfinally))
(defstruct (midir:seq statements))

; Monad-ish thing for composing statements/expressions
(defstruct (MidirExpr expr))
(defstruct (MidirStatement statement expr))
(defun (midir-splice m1 m2f)
  (destruct m1
    (MidirExpr expr1)
      (m2f expr1)
    (MidirStatement statement1 expr1)
      (destruct (m2f expr1)
        (MidirExpr expr2)
          (MidirStatement statement1 expr2)
        (MidirStatement statement2 expr2)
          (MidirStatement (midir:seq [statement1 statement2]) expr2))))
(defun (midir-join m1 m2)
  (destruct m2
    (MidirExpr expr2)
      (MidirStatement (midir-close m1) expr2)
    (MidirStatement statement2 expr2)
      (MidirStatement (midir:seq [(midir-close m1) statement2])
                      expr2)))
(defun (midir-close m)
  (destruct m
    (MidirExpr (midir:nop))
      (midir:seq '())
    (MidirExpr expr)
      (midir:expr expr)
    (MidirStatement stat (midir:nop))
      stat
    (MidirStatement stat expr)
      (midir:seq [stat (midir:expr expr)])))
(defun (midir-unlist ms)
  (defun (-unlist m)
    (destruct m
      (MidirExpr expr)
        [[] expr]
      (MidirStatement stat expr)
        [[stat] expr]))
  (let (ul (apply zip (map -unlist ms)))
    (if (nil? ul)
      [[] []]
      [(apply append (head ul)) (head (tail ul))])))
(defun (midir-set v expr)
  (MidirStatement (midir:expr (midir:binop "=" (midir:lookup v) expr)) (midir:nop)))

(defun (make-js-env parent # bindings)
  (let (d (apply dict bindings))
    (dfun
      (:lookup k)
        (if (dict:has? d k)  (midir:lookup (dict:get d k))
            (null? parent)   (error (++ "Environment key error: " (repr k)))
            otherwise        (parent :lookup k))
      (:has? k)
        (if (dict:has? d k)  @true
            (null? parent)   @false
            (parent :has? k))
      (:set! k v)
        (dict:set! d k v))))


(defun (compile-to-midir-toplevel exp genv igen)
  (destruct exp
    ['js:def v val]       (do (genv :unwarn-var v)
                              (compile-to-midir
                                `(js:seq ((js:binop "=" ,v ,val)
                                          @null))
                                (make-js-env (genv :as-js-env)) igen))
    ['js:seq statements]  (foldl (fun (acc next)
                                   (midir-join acc
                                               (compile-to-midir-toplevel next genv igen)))
                                 (MidirExpr (midir:nop))
                                 statements)
    _                     (compile-to-midir exp (make-js-env (genv :as-js-env)) igen)))

(defun (compile-to-midir exp env igen)
  (if (self-evaluating? exp)  (MidirExpr (midir:lit exp))
      (symbol? exp)           (MidirExpr (env :lookup exp))
;; (if (env :has? exp)
;;                                     (MidirExpr (midir:lookup (env :lookup exp)))
;;                                   otherwise
;;                                     (MidirExpr (midir:prop (midir:lookup "globals")
;;                                                            (midir:lit (symbol->string exp)))))
      (cons? exp)
        (compile-form-to-midir exp env igen)
      (error (++ "Unknown expression " (repr exp)))))

(defun (compile-form-to-midir exp env igen)
  (defun (comp e) (compile-to-midir e env igen))
  (destruct exp
    ['quote q]       (MidirExpr (midir:lit q))
    ['js:def v val]  (let (var (igen :get v))
                       (env :set! v var)
                       (midir-splice (comp val)
                         (fun (e) (MidirStatement (midir:def var e) (midir:nop)))))
    ['js:if expr con alt]
                     (let (outvar (igen :new "if"))
                       (midir-splice (comp expr)
                         (fun (ce)
                           (MidirStatement
                             (midir:seq
                               [(midir:def outvar (midir:nop))
                                (midir:if ce
                                  (midir-close (midir-splice (comp con)
                                                 (fun (be) (midir-set outvar be))))
                                  (midir-close (midir-splice (comp alt)
                                                 (fun (ae) (midir-set outvar ae)))))])
                             (midir:lookup outvar)))))
    ['js:while expr body]
                (let (outvar (igen :new "while"))
                  (midir-splice (comp expr)
                    (fun (ce)
                      (MidirStatement
                        (midir:seq
                          [(midir:def outvar ce)
                           (midir:while (midir:lookup outvar)
                             (midir:seq
                               [(midir-close (comp body))
                                (midir-close (midir-splice (comp expr)
                                               (fun (ce) (midir-set outvar ce))))]))])
                        (midir:nop)))))
    ['js:return expr]  (midir-splice (comp expr)
                         (fun (e)
                           (MidirStatement (midir:return e) (midir:nop))))
    ['js:break]     (MidirStatement (midir:break) (midir:nop))
    ['js:continue]  (MidirStatement (midir:continue) (midir:nop))
    ['js:seq exprs] (foldl (fun (acc next)
                             (midir-join acc (comp next)))
                           (MidirExpr (midir:nop))
                           exprs)
    ['js:js code]   (MidirExpr (midir:js code))
    ['js:code code] (MidirStatement (midir:code code) (midir:nop))
    ['js:binop op exprl exprr]
               (midir-splice (comp exprl)
                 (fun (el)
                   (midir-splice (comp exprr)
                     (fun (er)
                       (MidirExpr (midir:binop op el er))))))
    ['js:unop op expr]
               (midir-splice (comp expr)
                 (fun (e)
                   (MidirExpr (midir:unop op e))))
    ['js:prop expr iexpr]
               (midir-splice (comp expr)
                 (fun (e)
                   (midir-splice (comp iexpr)
                     (fun (ie)
                       (MidirExpr (midir:prop e ie))))))
    ['js:throw expr]
               (midir-splice (comp expr)
                  (fun (el)
                   (MidirStatement (midir:throw el) (midir:nop))))
    ['js:try texp cvar cexp fexp]
                 (let (cv   (igen :get cvar)
                       outvar (igen :new "try"))
                   (let (env2 (make-js-env env cvar cv))
                     (MidirStatement
                       (midir:seq
                         (list
                           (midir:def outvar (midir:nop))
                           (midir:try
                             (midir-close (midir-splice (comp texp)
                                            (fun (te) (midir-set outvar te))))
                             cv
                             (midir-close (midir-splice
                                            (compile-to-midir cexp env2 igen)
                                            (fun (ce) (midir-set outvar ce))))
                             (if (null? fexp)
                                 @null
                                 (midir-close (comp fexp))))))
                       (midir:lookup outvar))))
    ['js:function name args body]
                 (let* (argsv (map (fun (a) (igen :get a)) args)
                        argse (if (null? name) (nth exp 2) (cons name args))
                        env2 (apply make-js-env (cons env (map-append (fun (v) [v (igen :get v)])
                                                                      argse))))
                   
                   (MidirExpr
                     (midir:function (if (null? name) @null (igen :get name)) argsv
                       (midir-close
                         (midir-splice
                           (compile-to-midir body env2 igen)
                           (fun (e) (MidirStatement (midir:return e)
                                                    (midir:nop))))))))
    _    (midir-splice (comp (head exp))
           (fun (f)
             (let (ul (midir-unlist (map comp (tail exp)))
                   outvar (igen :new "funcall"))
               (MidirStatement
                 (midir:seq
                   [(midir:seq (head ul))
                    (midir:def outvar (midir:funcall f (head (tail ul))))])
                 (midir:lookup outvar)))))))


;;; Generating js

(defun (midir-expr-to-js m)
  (destruct m
    (midir:js code)
      (++ "(" code ")")
    (midir:binop op expl expr)
      (++ "(" (midir-expr-to-js expl) " " op " " (midir-expr-to-js expr) ")")
    (midir:unop op exp)
      (++ "(" op " " (midir-expr-to-js exp) ")")
    (midir:prop exp iexp)
      (++ "(" (midir-expr-to-js exp) "[" (midir-expr-to-js iexp) "])")
    (midir:lit s)
      (++ "(" (midir-lit-to-js s) ")")
    (midir:lookup s)
      s
    (midir:funcall f args)
      (++ "(" (midir-expr-to-js f) "(" (s:join ", " (map midir-expr-to-js args)) "))")
    (midir:function mname args body)
      (++ "(function "
          (if (null? mname) "" mname)
          "(" (s:join ", " args) ") {\n"
          (midir-statement-to-js body)
          "})")
    (midir:nop)
      "(null)"
    (error (++ "Unknown expr: " (repr m)))))

(defun (midir-statement-to-js m)
  (destruct m
    (midir:code code)
      (++ code ";\n")
    (midir:expr expr)
      (++ (midir-expr-to-js expr) ";\n")
    (midir:def var expr)
      (++ "var " var " = " (midir-expr-to-js expr) ";\n")
    (midir:if expr bcons balt)
      (++ "if (" (midir-expr-to-js expr) ") {\n"
          (midir-statement-to-js bcons)
          "} else {\n"
          (midir-statement-to-js balt)
          "}\n")
    (midir:while expr body)
      (++ "while (" (midir-expr-to-js expr) ") {\n"
          (midir-statement-to-js body)
          "}\n")
    (midir:return expr)
      (++ "return " (midir-expr-to-js expr) ";\n")
    (midir:break)
      "break;\n"
    (midir:continue)
      "continue;\n"
    (midir:throw expr)
      (++ "throw " (midir-expr-to-js expr) ";\n")
    (midir:try ts var cs mfinally)
      (++ "try {\n"
          (midir-statement-to-js ts)
          "} catch (" var ") {\n"
          (midir-statement-to-js cs)
          "}"
          (if (null? mfinally)
              ""
              (++ " finally {\n" (midir-statement-to-js mfinally) "}"))
          "\n")
    (midir:seq statements)
      (s:join "" (map midir-statement-to-js statements))
    (error (++ "Unknown statement: " (repr m)))))

(defun (midir-lit-to-js l)
  (if (nil? l)    "NilInstance"
      (cons? l)   (++ "new Cons("
                      (midir-lit-to-js (head l))
                      ", "
                      (midir-lit-to-js (tail l))
                      ")")
      (null? l)   "null"
      (boolean? l) (if l "true" "false")
      (undefined? l) "void 0"
      (symbol? l) (++ "Symbol.create(" (js-escape-string (symbol->string l))
                      (if (gensym? l) (error "Can't compile non-interned symbol") "") ; todo: figure out
                      ")")
      (number? l) (js:toString l)
      (string? l) (js-escape-string l)
      otherwise   (error (++ "Cannot compile literal: " (repr l)))))

(defun (clean-js-identifier s)
  (defun (-ci i)
    (if (< i (s:length s))
        (cons (if (or (and (<= "A" (s:charAt s i)) (<= (s:charAt s i) "Z"))
                      (and (<= "a" (s:charAt s i)) (<= (s:charAt s i) "z"))
                      (and (<= "0" (s:charAt s i)) (<= (s:charAt s i) "9"))
                      (= "$" (s:charAt s i)))
                (s:charAt s i)
                "_")
          (-ci (+ i 1)))
        '()))
  (apply ++ (-ci 0)))

(defun (make-js-ident-gen)
  (let (ids (dict)
        nextnum 0)
    (dfun
      (:get symb)
        (if (dict:has? ids symb)
          (dict:get ids symb)
          (let (id (++ (clean-js-identifier (symbol->string symb)) "_" nextnum))
            (set! nextnum (+ 1 nextnum))
            (dict:set! ids symb id)
            id))
      (:new prefix)
        (do1 (++ prefix "_" nextnum)
             (set! nextnum (+ 1 nextnum))))))


;;; Compilation

(defun (eval s)
  (eval-in-env s global-env))

(def eval-print-macro-expand @false
     eval-print-midir @false
     eval-print-js @false)

(defun (eval-in-env s env)
  (env :clear-warned)
  (let* (s2 (macro-expand s env)
         midir (midir-close (compile-to-midir-toplevel s2 env (make-js-ident-gen)))
         js (midir-statement-to-js midir))
    (when eval-print-macro-expand (println s2))
    (when eval-print-midir (println midir))
    (when eval-print-js (println js))
    (env :eval js)))

(defun (compile-stream s)
  (let (res @null)
    (while (read:has-another? s)
      (global-env :eval (++ "// " (SourcePos-show (s :pos))))
      ;; rebuild reader table from global-env here
      (set! res (eval-in-env (read s) global-env)))
    res))

(defun (compile-file fn)
  (let (s (stream:make fn (load-file fn)))
    (compile-stream s)))
(global-env :set! 'compile-file compile-file)

(defun (compile-string str)
  (let (s (stream:make "<input>" str))
    (compile-stream s)))


(defun (write-code output-file global-env)
  (def code (++ "module.exports = (function () {\nvar globals={};\n\n"
                (global-env :code)
                "return globals;"
                "\n})();\n"))
  (when (global-env :has? "main")
    (set! code (++ code "if (!module.parent) module.exports.main(module.exports.list.apply(void 0, process.argv.slice(2)));\n")))
  (write-file output-file code))

(defun (write-file filename content)
  (let (fs (js:js "require('fs')"))
    ((js:method fs "writeFileSync") filename content {'encoding "utf8"})
    @null))
(defun (load-file filename)
  (let (fs (js:js "require('fs')"))
    ((js:method fs "readFileSync") filename {'encoding "utf8"})))

(def load load-file)

(def repl-run-code compile-string)

(def $ @null
     $$ @null
     $$$ @null)


(defun (make-stdin-collector)
  (defun (do-eval code)
    (try (let (r (repl-run-code code))
           (pset! $$$ $$
                  $$ $
                  $ r)
           (unless (null? r)
             (println r)))
       :catch x
         (do (println (prop x "stack"))))
    (print "> "))

  (let (stdin (js:js "process.stdin")
        utterances '()
        inner @false)
    (println "jslisp. version: " compiler-version)
    (println)
    (print "> ")
    (js:call stdin 'resume)
    (js:call stdin 'setEncoding "utf8")
    (js:call stdin 'on "data"
      (fun (line)
        (if (= line "@INFERIOR-JSLISP-SEPARATOR\n")
            (let (code (apply ++ (reversed utterances)))
              (set! utterances '())
              (set! inner @false)
              (do-eval code))
            inner
              (do (push! utterances line))
            (= line "@INFERIOR-JSLISP-SEPARATOR-START\n")
              (set! inner @true)
            (do-eval line))))))

(defun (run-repl)
  (let* (require (js:js "require")
         readline (require "readline")
         rl (.createInterface readline
              {'input  (js:js "process.stdin")
               'output (js:js "process.stdout")})
         console (js:js "console")
         shouldQuit @false)

    (println "jslisp. version: " compiler-version)
    (println)

    (doto rl
      (.setPrompt "> ")
      (.prompt))

    (defun (handleLine line)
      (try (let (r (repl-run-code line))
             (pset! $$$ $$
                    $$ $
                    $ r)
             (unless (null? r)
               (println r)))
        :catch x
          (.log console x)))
;          (.log console (prop x "stack"))))

    (doto rl
      (.on "line"
       (fun (line)
         (set! shouldQuit @false)
         (handleLine line)
         (.prompt rl)))
      (.on "close"
       (fun () (println "Goodbye!")))
      (.on "SIGINT"
       (fun ()
         (if shouldQuit
           (do (println "Goodbye!")
               (js:code "process.exit(0);"))
           (do (set! shouldQuit @true)
               (println "Press C-c again to quit.")
               (.prompt rl))))))))

(defun (main args)
  (def input-files '())
  (def output-file @null)
  (def run-repl? @false)
  (def use-readline? @true)
  (defun (option-parser args)
    (when (not (nil? args))
      (switch (head args)
        "-o"  (do (set! args (tail args))
                  (set! output-file (head args)))
        "-no-rl"
              (set! use-readline? @false)
        "-r"  (set! run-repl? @true)
        "-pj" (set! eval-print-js @true)
        "-pm" (set! eval-print-macro-expand @true)
        (do (set! input-files (cons (head args) input-files))))
      (option-parser (tail args))))
  (option-parser args)
  (for fn in (reversed input-files)
    (print "Compiling " fn "\n")
    (compile-file fn))
  (unless (null? output-file)
    (when (nil? input-files)
      (error "Does not make sense to output file without input."))
    (write-code output-file global-env)
    (print "Output code to " output-file "\n"))
  (when (or (nil? input-files) run-repl?)
    (if use-readline?
        (run-repl)
        (make-stdin-collector))))
